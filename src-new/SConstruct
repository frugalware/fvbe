from os import environ

env = Environment()

# Set default optimizations if environment doesn't specify any.
if not 'CXXFLAGS' in environ:
	env.Append(CXXFLAGS = [ '-O2' ])
else:
	env.Append(CXXFLAGS = environ['CXXFLAGS'])

# Inherit linker flags from environment.
if 'LDFLAGS' in environ:
	env.Append(LINKFLAGS = environ['LDFLAGS'])

# Enable most warnings
env.Append(CXXFLAGS = [ '-Wall', '-Wextra' ])

# Enable debug symbols or disable debugging code.
debug = int(ARGUMENTS.get('debug', 1))
if debug:
	env.Append(CXXFLAGS = [ '-g' ])
else:
	env.Append(CXXFLAGS = [ '-DNDEBUG' ])

# Disable the prefix / suffix for shared libraries.
env['SHLIBPREFIX'] = ''
env['SHLIBSUFFIX'] = ''

# libfvbe build instructions
ui_o = env.SharedObject('ui.o', 'ui.cpp')
libfvbe = env.SharedLibrary('libfvbe.so', [ ui_o ])

# ui_newt build instructions
ui_newt_o = env.SharedObject('ui_newt.o', 'ui_newt.cpp')
ui_newt = env.SharedLibrary('ui_newt.so', [ ui_newt_o, libfvbe ], LIBS = '-lnewt')

# main build instructions
main_o = env.Object('main.o', 'main.cpp')
main = env.Program('main', [ main_o, libfvbe ], LIBS = '-ldl')
